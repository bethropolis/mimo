show "=== Decorators ==="

@logged
function compute(x)
    return * x x
end

function logged(target)
    return function(...args)
        show + "Calling with " (call len(args))
        return call target(...args)
    end
end

call compute(4) -> square
show square

function retry(times)
    return function(target)
        return function(...args)
            let attempts 0
            while < attempts times
                try
                    return call target(...args)
                catch e
                    set attempts + attempts 1
                    if >= attempts times
                        throw e
                    end
                end
            end
            return null
        end
    end
end

set unstable_attempts 0

@retry(3)
function unstable()
    set unstable_attempts + unstable_attempts 1
    if < unstable_attempts 3
        throw "temporary"
    end
    return "ok"
end

show call unstable()

function bold(target)
    return function(...args)
        return + "**" + call target(...args) "**"
    end
end

function italic(target)
    return function(...args)
        return + "_" + call target(...args) "_"
    end
end

@bold
@italic
function label(name)
    return name
end

show call label("Mimo")

@logged
export function exported(name)
    return + "Hi " name
end

show call exported("World")
show "=== Done ==="
